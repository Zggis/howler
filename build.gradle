import groovy.json.JsonSlurper

plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
}

def packageSlurper = new JsonSlurper()
def packageJson = packageSlurper.parse file('frontend/package.json')
version = packageJson.version
group = 'com.zggis'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.13'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'commons-io:commons-io:2.11.0'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

task deleteStaticFolder(type: Delete) {
    def dirName = "src/main/resources/static"
    file( dirName ).list().each{
        f ->
            delete "${dirName}/${f}"
    }
}

assemble.dependsOn('deleteStaticFolder','frontend:build')

jar {
    enabled = false
}
